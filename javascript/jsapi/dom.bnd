/* DOM bindings entries */

webidlfile "dom.idl";

/* interface Node members */

getter nodeType %{
	dom_exception exc;
	dom_node_type node_type;

	exc = dom_node_get_node_type(private->node, &node_type);
	if (exc != DOM_NO_ERR) {
		return JS_FALSE;
	}
	jsret = node_type;
%}


getter nodeName %{
	dom_exception exc;
	dom_string *name;

	exc = dom_node_get_node_name(private->node, &name);
	if (exc != DOM_NO_ERR) {
		return JS_FALSE;
	}

	if (name != NULL) {
		jsret = JS_NewStringCopyN(cx,
					  dom_string_data(name),
					  dom_string_length(name));
		dom_string_unref(name);
	}
%}

getter nodeValue %{
	dom_exception exc;
	dom_string *value;

	exc = dom_node_get_node_value(private->node, &value);
	if (exc != DOM_NO_ERR) {
		return JS_FALSE;
	}

	if (value != NULL) {
		jsret = JS_NewStringCopyN(cx,
					  dom_string_data(value),
					  dom_string_length(value));
		dom_string_unref(value);
	}
%}

getter textContent %{
	dom_exception exc;
	dom_string *content;

	exc = dom_node_get_text_content(private->node, &content);
	if (exc != DOM_NO_ERR) {
		return JS_FALSE;
	}

	if (content != NULL) {
		jsret = JS_NewStringCopyN(cx, dom_string_data(content), dom_string_length(content));
		dom_string_unref(content);

	}
%}


operation appendChild %{
	struct dom_node *result = NULL;
	dom_exception exc;

	struct jsclass_private *node_private;
	dom_node_type node_type;

	JSLOG("appending %p", node);

	/* CAUTION this expects all Node objects private pointers to
	 * have private->node in the same place
	 */
	/* text */
	node_private = JS_GetInstancePrivate(cx, node, &JSClass_Text, NULL);
	if (node_private == NULL) {
		/* element */
		node_private = JS_GetInstancePrivate(cx, node, &JSClass_HTMLElement, NULL);
	}

	if (node_private == NULL) {
		/* type error? */
		return JS_FALSE;
	}

	/* append the found element */
	exc = dom_node_append_child(private->node, node_private->node, &result);
	if (exc != DOM_NO_ERR) {
		return JS_FALSE;
	}

	if (result != NULL) {
		exc = dom_node_get_node_type(result, &node_type);
		if (exc != DOM_NO_ERR) {
			return JS_FALSE;
		}
		switch (node_type) {
		case DOM_ELEMENT_NODE:
			jsret = jsapi_new_HTMLElement(cx, NULL, NULL, (dom_element *)result, private->htmlc);
			break;

		case DOM_TEXT_NODE:
			jsret = jsapi_new_Text(cx, NULL, NULL, (dom_text *)result, private->htmlc);
			break;

		default:
			JSLOG("Unsupported result node type %d", node_type);
		}

	} else {
		JSLOG("No result");
	}
%}
